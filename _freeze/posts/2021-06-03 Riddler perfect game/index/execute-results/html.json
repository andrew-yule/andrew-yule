{
  "hash": "9cbc80102f7a7a3924f58ad1b4f5a9a6",
  "result": {
    "markdown": "# On-base percentage for a certain number of perfect games\n\nI always enjoy reading the weekly riddler puzzles from 538 ([link](https://fivethirtyeight.com/tag/the-riddler/)). My background is not in statistics, but I enjoy the puzzles because they're typically just the right amount of complexity to solve in a reasonable time frame and the topics are interesting and wide-ranging. When there is one involving sports, it's even better.\n\nHere's the riddler express from June 3rd 2021 discussing baseball perfect games:\n\n\n>*\"How low would a batter's chances of reaching base have to be for you to expect one perfect game per season? (You can make the following simplifying assumptions: All batters have the same chances of reaching base; at-bats are independent from each other; there are 30 MLB teams, and each club plays 162 games; and no games go into extra innings.)\"*\n>\n\nMy preferred computational tool is the Wolfram Language so I'll take a stab at solving this riddler using it.\n\nFirst, let's define some global constants based on what we are allowed to assume.\n\n```\ngamesPerYear = 162;\nteams = 30;\nnumberOfInnings = 9;\natBatsPerInning = 3;\natBatsDuringAGame = numberOfInnings*atBatsPerInning;\n```\n\n```\ncreateDist[onBasePct_] := \n EmpiricalDistribution[{1 - onBasePct, onBasePct} -> {0, 1}]\n```\n\n Simulating a full season requires us to randomly sample our on-base distribution for the total number of at bats that we can expect for every game played across each team. Then, to ensure we account for randomness correctly, because this is a simulation, we need to repeat this process a number of times. The more we perform a simulation, averaging the results, the better our prediction will be.\n\nRandomVariate lets us sample our distribution a number of times and create nested arrays. In this case we simulate the number of at bats in a game for each team across an entire season 100 times. Then for each simulation we count the number of perfect games we have and take an average across the full number of season simulations.\n\n```\nnumberOfSeasonSimulations = 100;\n\nsimulateFullSeason[onBasePct_] := \n Module[{dist, data, perfectGameCount},\n  \tdist = createDist[onBasePct];\n  \tdata = RandomVariate[createDist[onBasePct], {\n     \t\tnumberOfSeasonSimulations,\n     \t\tteams*gamesPerYear,\n     \t\tatBatsDuringAGame\n     \t}];\n  \tperfectGameCount = Map[Count[x_ /; AllTrue[x, EqualTo[0]]]]@data;\n  \tN@Mean[perfectGameCount]\n  ]\n```\n\nSimulating each season multiple times for various on-base averages let's us see how the number of predicted perfect games in a season varies.\n\n```\nresults = \n Table[{onBasePct, simulateFullSeason[onBasePct]}, {onBasePct, 0.1, \n   0.4, 0.01}]\n```\n\n```\n{{0.1, 282.13}, {0.11, 209.22}, {0.12, 154.54}, {0.13, 114.1}, {0.14, \n  83.69}, {0.15, 61.1}, {0.16, 44.35}, {0.17, 31.48}, {0.18, \n  22.6}, {0.19, 16.87}, {0.2, 11.67}, {0.21, 8.46}, {0.22, \n  5.96}, {0.23, 4.16}, {0.24, 2.9}, {0.25, 2.34}, {0.26, 1.51}, {0.27,\n   0.98}, {0.28, 0.64}, {0.29, 0.49}, {0.3, 0.33}, {0.31, \n  0.27}, {0.32, 0.16}, {0.33, 0.06}, {0.34, 0.07}, {0.35, \n  0.08}, {0.36, 0.03}, {0.37, 0.03}, {0.38, 0.03}, {0.39, 0.}, {0.4, \n  0.}}\n```\n\n```\nListLinePlot[\n \tresults,\n \tAxesLabel -> {\"League On-Base Average\", \n   \"Predicted Number of\\nPerfect Games in a Season\"},\n \tImageSize -> 500\n ]\n```\n![](Image1.png)\n\nAnd finally, using the results we can calculate the required on-base average that would lead to one perfect game per season\n\n```\nFindRoot[Interpolation[results][onBasePct] == 1, {onBasePct, 0.2}]\n```\n```\n{onBasePct -> 0.269623}\n```\n\n",
    "supporting": [
      "index_files"
    ],
    "filters": [],
    "includes": {}
  }
}